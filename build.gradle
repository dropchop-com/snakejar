import org.gradle.internal.jvm.Jvm
import org.gradle.internal.os.OperatingSystem;

plugins {
  id 'java'
  id 'application'
  id 'c'
  id 'maven-publish'
  id 'org.graalvm.buildtools.native' version "0.9.5" // or a newer version
}

def pyVersionLibPath() {
  def out = new ByteArrayOutputStream()
  def resourcesDir = sourceSets.main.resources.srcDirs.first().absolutePath
  def scriptFile = file("${resourcesDir}/pyverlib.py").absolutePath;
  exec {
    commandLine "python", scriptFile, '2>&1'
    standardOutput = out
  }
  def ret = out.toString().trim()
  def (python_version, python_lib_path) = ret.tokenize(';;')
  def (major_num, minor_num, build_num) = python_version.trim().tokenize('.')

  project.ext.python_version_major = major_num
  project.ext.python_version_minor = minor_num
  if (minor_num == null) {
    project.ext.python_version = major_num
  } else {
    project.ext.python_version = major_num + "." + minor_num
  }
  project.ext.python_lib_path = python_lib_path
  project.ext.python_lib_dir = new File(python_lib_path).parentFile.getAbsolutePath()
  return ret
}

pyVersionLibPath()

sourceCompatibility = 17
targetCompatibility = 17
mainClassName = "com.dropchop.snakejar.Example"
applicationDefaultJvmArgs.add("-Djava.library.path=" + file("${buildDir}/libs/snakejar/shared").absolutePath)
applicationDefaultJvmArgs.add("-Dsnakejar.log.level=error")
applicationDefaultJvmArgs.add("-agentlib:native-image-agent=config-output-dir=./agent")

println "Java Library Path: ${System.properties.get('java.library.path')}"
println "Python version: ${python_version}"
println "Python Library Path: ${python_lib_path}"
println "Java Home: ${Jvm.current().javaHome}"

allprojects {
  tasks.withType(Test) {
    systemProperty 'file.encoding', 'UTF-8'
  }

  tasks.withType(JavaExec) {
    systemProperty 'file.encoding', 'UTF-8'
  }
}

compileJava {
  options.encoding = 'UTF-8'
}

compileTestJava {
  options.encoding = 'UTF-8'
}

javadoc {
  options.encoding = 'UTF-8'
}

repositories {
  mavenCentral()
  gradlePluginPortal()
}

dependencies {
  implementation 'org.slf4j:slf4j-api:1.7.32'
  implementation 'ch.qos.logback:logback-classic:1.2.9'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
  systemProperty "java.library.path", file("${buildDir}/libs/snakejar/shared").absolutePath
  systemProperty "snakejar.log.level", "debug"
  testLogging.showStandardStreams = true
  useJUnitPlatform()
}

task copyLibs(type: Copy) {
  from("${buildDir}/libs/snakejar/shared/") {
    include 'lib*.so'
    include '*.dll'
    include 'lib*.dynlib'
  }
  //into "${sourceDir}/main/resources/"
  into project.sourceSets.main.resources.srcDirs[0]
}

jar {
  from("${buildDir}/prep/") {
    include 'lib*.so'
    include 'lib*.dll'
    include 'lib*.dynlib'
  }
}

javadoc {
  source = sourceSets.main.allJava
  exclude 'com/dropchop/snakejar/embed/**'
  exclude 'com/dropchop/snakejar/maybe/**'
  options.addStringOption('Xdoclint:none', '-quiet')
  dependsOn jar
}

tasks.register('javadocJar', Jar) {
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
  dependsOn javadoc
}

tasks.register('sourcesJar', Jar) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
  dependsOn javadoc
}

graalvmNative.binaries.main {
  imageName = "snakejar"
  mainClass = "com.dropchop.snakejar.Example"
  verbose = true
  fallback = false
  systemProperties = ["java.library.path": file("${buildDir}/libs/snakejar/shared").absolutePath]
  runtimeArgs.add("-Djava.library.path=" + file("${buildDir}/libs/snakejar/shared").absolutePath)
  runtimeArgs.add("-Dsnakejar.log.level=error")
  buildArgs.add('--allow-incomplete-classpath')
  // build/native/nativeCompile/snakejar -Djava.library.path=build/libs/snakejar/shared -Dsnakejar.log.level=debug
  // build/native/nativeCompile/snakejar -Dsnakejar.log.level=debug
}

def pyIncludeDir() {
  def out = new ByteArrayOutputStream()
  exec {
    commandLine "python${python_version}", '-c', 'import sysconfig; print(sysconfig.get_paths()[\'include\'])', '2>&1'
    standardOutput = out
  }
  return out.toString().trim()
}

@SuppressWarnings('unused')
def pySitePackagesDir() {
	def out = new ByteArrayOutputStream()
	exec {
		commandLine "python${python_version}", '-c', 'import site; print(site.getsitepackages()[0])', '2>&1'
		standardOutput = out
	}
	return out.toString().trim()
}

model {
  platforms {
    x64 {
      architecture "x86_64"
    }
//    x86 {
//      architecture "x86"
//    }
  }

  components {
    snakejar(NativeLibrarySpec) {
      // targetPlatform "x86"
      targetPlatform "x64"
      sources {
        c {
          source {
            srcDir "src/main/c"
            include "**/*.c"
          }
          exportedHeaders {
            srcDir "src/main/c/include"
          }
        }
      }
      if (OperatingSystem.current().isWindows()) {
        baseName = "${project.name}-win-py${python_version}-x64"
      }
      if (OperatingSystem.current().isLinux()) {
        baseName = "${project.name}-linux-py${python_version}-x64"
      }
      if (OperatingSystem.current().isMacOsX()) {
        baseName = "${project.name}-macos-py${python_version}-x64"
      }
      binaries.all {
        def pyIncludeDir = pyIncludeDir()
        if (targetPlatform.operatingSystem.macOsX) {
          cCompiler.args '-I', "${Jvm.current().javaHome}/include"
          cCompiler.args '-I', "${Jvm.current().javaHome}/include/darwin"
          cCompiler.args '-I', pyIncludeDir
          cCompiler.args '-mmacosx-version-min=10.4'
          linker.args '-mmacosx-version-min=10.4'
          linker.args "-lpython${python_version}"
          //linker.args '-o', "${buildDir}/libs/${project.name}/shared/lib${project.name}-linux-x64.dynlib"
        } else if (targetPlatform.operatingSystem.linux) {
          cCompiler.args '-I', "${Jvm.current().javaHome}/include"
          cCompiler.args '-I', "${Jvm.current().javaHome}/include/linux"
          cCompiler.args '-I', pyIncludeDir
          cCompiler.args '-g' // for debug
          cCompiler.args '-D_FILE_OFFSET_BITS=64'
          cCompiler.args '-DJEP_NUMPY_ENABLED=0'
          linker.args "-lpython${python_version}"
          linker.args "-rdynamic"
          //linker.args '-o', "${buildDir}/libs/${project.name}/shared/lib${project.name}-linux-py${python_version}-x64.so"
        } else if (targetPlatform.operatingSystem.windows) {
          cCompiler.args "/I${Jvm.current().javaHome}/include"
          cCompiler.args "/I${Jvm.current().javaHome}/include/win32"
          cCompiler.args "/I${pyIncludeDir}"
          cCompiler.args "/D_WINDLL"
          cCompiler.args "/DWIN32"
          cCompiler.args '/D_FILE_OFFSET_BITS=64'
          cCompiler.args '/DJEP_NUMPY_ENABLED=0'
          linker.args "/LIBPATH:${python_lib_dir}/libs"
          linker.args "/LIBPATH:${Jvm.current().javaHome}/lib"
          linker.args "/IMPLIB:python${python_version}.lib"
          linker.args "/IMPLIB:jvm.lib"
          linker.args "/MACHINE:X64"
          //linker.args "/OUT:${project.name}-win-py${python_version}-x64.dll"
        }
      }
    }
  }
}

nativeRun.dependsOn 'snakejarSharedLibrary'
copyLibs.dependsOn 'snakejarSharedLibrary'
jar.dependsOn 'copyLibs'
run.dependsOn 'snakejarSharedLibrary'
test.dependsOn 'snakejarSharedLibrary'
assemble.dependsOn javadocJar
assemble.dependsOn sourcesJar
